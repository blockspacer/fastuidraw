/*!
 * \file fastuidraw_painter_brush.frag.glsl.resource_string
 * \brief file fastuidraw_painter_brush.frag.glsl.resource_string
 *
 * Copyright 2018 by Intel.
 *
 * Contact: kevin.rogovin@intel.com
 *
 * This Source Code Form is subject to the
 * terms of the Mozilla Public License, v. 2.0.
 * If a copy of the MPL was not distributed with
 * this file, You can obtain one at
 * http://mozilla.org/MPL/2.0/.
 *
 * \author Kevin Rogovin <kevin.rogovin@intel.com>
 *
 */

float
fastuidraw_brush_apply_spread(in float t, in float range,
                              in uint spread_type)
{
  if (spread_type == fastuidraw_brush_spread_mirror)
    {
      t = fastuidraw_compute_mirror_spread(t, range);
    }
  else if (spread_type == fastuidraw_brush_spread_repeat)
    {
      t = fastuidraw_compute_repeat_spread(t, range);
    }
  else if (spread_type == fastuidraw_brush_spread_mirror_repeat)
    {
      t = fastuidraw_compute_mirror_repeat_spread(t, range);
    }
  else
    {
      t = fastuidraw_compute_clamp_spread(t, range);
    }
  return t;
}

float
fastuidraw_brush_compute_interpolate(in vec2 p, in uint gradient_type, out float good)
{
  float t;

  t = 1.0;
  if (gradient_type == fastuidraw_brush_radial_gradient_type)
    {
      fastuidraw_radial_gradient g;

      g.p0 = vec2(fastuidraw_brush_gradient_p0_x, fastuidraw_brush_gradient_p0_y);
      g.p1 = vec2(fastuidraw_brush_gradient_p1_x, fastuidraw_brush_gradient_p1_y);
      g.r0 = fastuidraw_brush_gradient_r0;
      g.r1 = fastuidraw_brush_gradient_r1;

      t = fastuidraw_compute_interpolate(p, g, good);
    }
  else if (gradient_type == fastuidraw_brush_linear_gradient_type)
    {
      fastuidraw_linear_gradient g;

      g.p0 = vec2(fastuidraw_brush_gradient_p0_x, fastuidraw_brush_gradient_p0_y);
      g.p1 = vec2(fastuidraw_brush_gradient_p1_x, fastuidraw_brush_gradient_p1_y);

      good = 1.0;
      t = fastuidraw_compute_interpolate(p, g);
    }
  else if (gradient_type == fastuidraw_brush_sweep_gradient_type)
    {
      fastuidraw_sweep_gradient g;

      g.p = vec2(fastuidraw_brush_gradient_sweep_point_x, fastuidraw_brush_gradient_sweep_point_y);
      g.sweep_angle = fastuidraw_brush_gradient_sweep_angle;
      g.signed_factor = fastuidraw_brush_gradient_sweep_sign_factor;

      good = 1.0;
      t = fastuidraw_compute_interpolate(p, g);
    }

  return t;
}

#ifndef FASTUIDRAW_IMAGE_ATLAS_DISABLED

vec4
fastuidraw_brush_image_of_atlas(in vec2 q, in uint image_filter, in float lod)
{
  vec2 image_xy;
  fastuidraw_image_extended_info_type image_info;
  vec4 image_color;

  image_info.base.num_levels = fastuidraw_brush_image_number_index_lookups;
  image_info.base.image_texel_size_on_master_index_tile = fastuidraw_brush_image_texel_size_on_master_index_tile;
  image_info.base.recip_image_texel_size_on_master_index_tile = fastuidraw_brush_recip_image_texel_size_on_master_index_tile;
  image_info.image_size = vec2(fastuidraw_brush_image_size_x, fastuidraw_brush_image_size_y);
  image_info.master_index_tile_bottom_left = vec2(fastuidraw_brush_image_x, fastuidraw_brush_image_y);
  image_info.master_index_tile_layer = fastuidraw_brush_image_layer;

  /* clamp lod away from FASTUIDRAW_IMAGE_ATLAS_COLOR_TILE_LOG2_SIZE */
  lod = min(lod, float(FASTUIDRAW_IMAGE_ATLAS_COLOR_TILE_LOG2_SIZE) - 0.5);

  if (image_filter == uint(fastuidraw_brush_image_filter_nearest))
    {
      lod = ceil(lod);
      image_color = fastuidraw_nearest_filter_atlas_image(q, image_info, lod);
    }
  else if (image_filter == uint(fastuidraw_brush_image_filter_linear) || lod > 0.5)
    {
      lod = round(lod);
      image_color = fastuidraw_linear_filter_atlas_image(q, image_info, int(lod));
    }
  else
    {
      image_color = fastuidraw_cubic_filter_atlas_image(q, image_info);
    }

  return image_color;
}

#else

vec4
fastuidraw_brush_image_of_atlas(in vec2 q, in uint image_filter, in float lod)
{
  return vec4(1.0, 1.0, 1.0, 1.0);
}

#endif

vec4
fastuidraw_brush_image_of_texture(in sampler2D surface, in vec2 texel_coord, in uint image_filter, in float lod)
{
  vec4 image_color;

  texel_coord += vec2(fastuidraw_brush_image_x, fastuidraw_brush_image_y);
  if (image_filter == uint(fastuidraw_brush_image_filter_nearest))
    {
      image_color = fastuidraw_nearest_filter_texture(surface, texel_coord, lod);
    }
  else if (image_filter == uint(fastuidraw_brush_image_filter_linear) || lod > 0.5)
    {
      image_color = fastuidraw_linear_filter_texture(surface, texel_coord, lod);
    }
  else
    {
      image_color = fastuidraw_cubic_filter_texture(surface, texel_coord);
    }

  return image_color;
}

vec4
fastuidraw_brush_image_of_context_texture(in vec2 texel_coord, in uint image_filter, in float lod)
{
  return fastuidraw_brush_image_of_texture(fastuidraw_context_texture[0], texel_coord, image_filter, lod);
}

#if defined(FASTUIDRAW_SUPPORT_BINDLESS_TEXTURE)

vec4
fastuidraw_brush_image_of_bindless(in vec2 texel_coord, in uint image_filter, in float lod)
{
  #if defined(FASTUIDRAW_BINDLESS_HANDLE_UVEC2)
    uvec2 handle;
    handle.x = fastuidraw_brush_image_bindless_low_handle;
    handle.y = fastuidraw_brush_image_bindless_high_handle;
  #else
    uint64_t handle;
    handle = uint64_t(fastuidraw_brush_image_bindless_high_handle) << uint64_t(32u);
    handle |= uint64_t(fastuidraw_brush_image_bindless_low_handle);
  #endif

  sampler2D im = sampler2D(handle);
  return fastuidraw_brush_image_of_texture(im, texel_coord, image_filter, lod);
}

#else

vec4
fastuidraw_brush_image_of_bindless(in vec2 p, in uint image_filter, in float lod)
{
  return vec4(1.0, 1.0, 1.0, 1.0);
}

#endif

vec4
fastuidraw_gl_frag_brush_main(in uint sub_shader, in uint shader_data_offset)
{
  vec2 p, dpdx, dpdy;
  uint gradient_type;
  vec4 return_value = vec4(fastuidraw_brush_color_x,
                           fastuidraw_brush_color_y,
                           fastuidraw_brush_color_z,
                           fastuidraw_brush_color_w);

  p = vec2(fastuidraw_brush_p_x, fastuidraw_brush_p_y);
  dpdx = dFdx(p);
  dpdy = dFdy(p);

  /* TODO:
   *  have bits in x and y to specify the repeat modes
   *    - clamp
   *    - mirror
   *    - repeat
   *    - mirror-repeat
   */
  if ((fastuidraw_brush_features & uint(fastuidraw_brush_repeat_window_mask)) != 0u)
    {
      uint x_spread, y_spread;

      x_spread = FASTUIDRAW_EXTRACT_BITS(fastuidraw_brush_repeat_window_x_spread_type_bit0,
                                         fastuidraw_brush_gradient_type_num_bits,
                                         fastuidraw_brush_features);

      y_spread = FASTUIDRAW_EXTRACT_BITS(fastuidraw_brush_repeat_window_y_spread_type_bit0,
                                         fastuidraw_brush_gradient_type_num_bits,
                                         fastuidraw_brush_features);

      p -= vec2(fastuidraw_brush_repeat_window_x, fastuidraw_brush_repeat_window_y);
      p.x = fastuidraw_brush_apply_spread(p.x, fastuidraw_brush_repeat_window_w, x_spread);
      p.y = fastuidraw_brush_apply_spread(p.y, fastuidraw_brush_repeat_window_h, y_spread);
      p += vec2(fastuidraw_brush_repeat_window_x, fastuidraw_brush_repeat_window_y);
    }

  gradient_type = FASTUIDRAW_EXTRACT_BITS(fastuidraw_brush_gradient_type_bit0,
                                          fastuidraw_brush_gradient_type_num_bits,
                                          fastuidraw_brush_features);

  if (gradient_type != fastuidraw_brush_no_gradient_type)
    {
      float t, good;
      uint spread_type;

      t = fastuidraw_brush_compute_interpolate(p, gradient_type, good);
      spread_type = FASTUIDRAW_EXTRACT_BITS(fastuidraw_brush_gradient_spread_type_bit0,
                                            fastuidraw_brush_spread_type_num_bits,
                                            fastuidraw_brush_features);

      /* NOTE: there is a Mesa/i965 bug where if we change the
       * order of this if/else chain to something different,
       * it might not correctly evaluate.
       */
      if (spread_type == fastuidraw_brush_spread_mirror)
        {
          t = fastuidraw_compute_mirror_spread(t);
        }
      else if (spread_type == fastuidraw_brush_spread_repeat)
        {
          t = fastuidraw_compute_repeat_spread(t);
        }
      else if (spread_type == fastuidraw_brush_spread_mirror_repeat)
        {
          t = fastuidraw_compute_mirror_repeat_spread(t);
        }
      else
        {
          t = fastuidraw_compute_clamp_spread(t);
        }
      t = fastuidraw_brush_color_stop_x + t * fastuidraw_brush_color_stop_length;
      return_value *= (good * fastuidraw_colorStopFetch(t, fastuidraw_brush_color_stop_y));
    }

  /* apply alpha before doing image because image will multiply
   * pre-multiplied alpha value
   */
  return_value.rgb *= return_value.a;
  if ((fastuidraw_brush_features & uint(fastuidraw_brush_image_mask)) != 0u)
    {
      uint image_filter, image_type, mipmap_max_level;
      vec2 q;
      float lod;
      vec4 image_color;

      /* Clamp the image coordinate to the dimensions of the image;
       * Should we allow for repeat modes as well?
       */
      q.x = clamp(p.x, 0.0, float(fastuidraw_brush_image_size_x) - 1.0);
      q.y = clamp(p.y, 0.0, float(fastuidraw_brush_image_size_y) - 1.0);
      image_type = FASTUIDRAW_EXTRACT_BITS(fastuidraw_brush_image_type_bit0,
                                           fastuidraw_brush_image_filter_num_bits,
                                           fastuidraw_brush_features);

      image_filter = FASTUIDRAW_EXTRACT_BITS(fastuidraw_brush_image_filter_bit0,
                                             fastuidraw_brush_image_filter_num_bits,
                                             fastuidraw_brush_features);

      mipmap_max_level = FASTUIDRAW_EXTRACT_BITS(fastuidraw_brush_image_mipmap_bit0,
                                                 fastuidraw_brush_image_mipmap_num_bits,
                                                 fastuidraw_brush_features);

      if (mipmap_max_level > 0u)
        {
          float rho;
          rho = (dot(dpdx, dpdx) + dot(dpdy, dpdy)) * 0.5;
          lod = 0.5 * log2(max(1.0, rho));
          lod = min(float(mipmap_max_level), lod);
        }
      else
        {
          lod = 0.0;
        }

      if (image_type == uint(fastuidraw_brush_image_type_on_atlas))
        {
          image_color = fastuidraw_brush_image_of_atlas(q, image_filter, lod);
        }
      else if (image_type == uint(fastuidraw_brush_image_type_bindless_texture2d))
        {
          image_color = fastuidraw_brush_image_of_bindless(q, image_filter, lod);
        }
      else
        {
          image_color = fastuidraw_brush_image_of_context_texture(q, image_filter, lod);
        }

      /* BEWARE: we are just testing if the format bits are non-zero;
       * We get away with this for now because there are only two
       * image-formats: rgba and premultiplied_rgba with a non-zero
       * value indicateing that the rgb components are pre-multiplied.
       */
      if ((fastuidraw_brush_features & fastuidraw_brush_image_format_mask) == 0u)
        {
          image_color.rgb *= image_color.a;
        }

      return_value *= image_color;
    }

  return return_value;
}
